plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '2.0.0'
}

apply from: "$rootDir/gradle/functional-test.gradle"
apply from: "$rootDir/gradle/additional-artifacts.gradle"

group = 'com.bmuschko'
version = '3.2.1'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle IzPack plugin',
                   'Implementation-Version': archiveVersion,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version"),
                   'Built-Gradle': gradle.gradleVersion
    }
}

gradlePlugin {
    website = 'https://github.com/bmuschko/gradle-izpack-plugin'
    vcsUrl = 'https://github.com/bmuschko/gradle-izpack-plugin'

    plugins {
        izpackPlugin {
            id = 'com.bmuschko.izpack'
            displayName = 'Gradle IzPack Plugin'
            description = 'Plugin that provides support for packaging applications for the Java platform via IzPack.'
            tags.set(['packaging', 'distribution', 'installer'])
            implementationClass = 'com.bmuschko.gradle.izpack.IzPackPlugin'
        }
    }
}