plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
}

apply from: "$rootDir/gradle/functional-test.gradle"
apply from: "$rootDir/gradle/additional-artifacts.gradle"

group = 'com.bmuschko'
version = '3.2'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle IzPack plugin',
                   'Implementation-Version': archiveVersion,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version"),
                   'Built-Gradle': gradle.gradleVersion
    }
}

gradlePlugin {
    plugins {
        create('izpack-plugin') {
            id = 'com.bmuschko.izpack'
            displayName = 'Gradle IzPack Plugin'
            implementationClass = 'com.bmuschko.gradle.izpack.IzPackPlugin'
        }
    }
}

pluginBundle {
    website = "https://github.com/bmuschko/gradle-izpack-plugin"
    vcsUrl = "https://github.com/bmuschko/gradle-izpack-plugin"
    tags = ['packaging', 'distribution', 'installer']
}